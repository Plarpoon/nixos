{
  nixpkgs,
  nix-darwin,
  home-manager,
}:

{
  mkNixOS =
    {
      hostname,
      system,
      username,
    }:
    nixpkgs.lib.nixosSystem {
      inherit system;

      specialArgs = {
        inherit hostname username;
      };

      modules = [
        ./${hostname}

        home-manager.nixosModules.home-manager
        {
          home-manager.useGlobalPkgs = true;
          home-manager.useUserPackages = true;
          home-manager.backupFileExtension = "backup";

          home-manager.users.${username} = import ./${hostname}/home-manager;
        }
      ];
    };

  mkDarwin =
    {
      hostname,
      system,
      username,
    }:
    nix-darwin.lib.darwinSystem {
      inherit system;

      specialArgs = {
        inherit hostname username;
      };

      modules = [
        ./${hostname}

        home-manager.darwinModules.home-manager
        {
          home-manager.useGlobalPkgs = true;
          home-manager.useUserPackages = true;
          home-manager.backupFileExtension = "backup";

          home-manager.users.${username} = import ./${hostname}/home-manager;
        }
      ];
    };
}
